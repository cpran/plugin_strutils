# This script is part of the strutils CPrAN plugin for Praat.
# The latest version is available through CPrAN or at
# <http://cpran.net/plugins/strutils>
#
# The strutils plugin is free software: you can redistribute it
# and/or modify it under the terms of the GNU General Public
# License as published by the Free Software Foundation, either
# version 3 of the License, or (at your option) any later version.
#
# The strutils plugin is distributed in the hope that it will be
# useful, but WITHOUT ANY WARRANTY; without even the implied warranty
# of MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with strutils. If not, see <http://www.gnu.org/licenses/>.
#
# Copyright 2014-2016 Jose Joaquin Atria

#! ~~~ params
#! in:
#!   .search$:  Query string
#!   .regex: >
#!     Boolean flag. If true, interpret query string as regular expression
#! out:
#!   .return: Index of first matching string if found, 0 if not
#! selection:
#!   in:
#!     strings: 1
#! ~~~
#!
#! Searches for a string in a Strings object. The string matching can be
#! done using string literals (which will match if the occur anywhere in the
#! string), or regular expression patterns. This behaviour can be adjusted
#! by modifying the value of `.regex`.
#!
procedure findInStrings (.search$, .regex)
  .id = selected("Strings")
  .strings = Get number of strings
  .match = 0
  for .i to .strings
    .string$ = Get string: .i
    .match = if .regex
      ... then index_regex(.string$, .search$)
      ... else if index(.string$, .search$) then .i else 0 fi fi
    if .match
      .match = .i
      goto LAST
    endif
  endfor

  label LAST
  .return = .match
endproc
